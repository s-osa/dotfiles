setopt IGNORE_EOF
setopt INC_APPEND_HISTORY
setopt EXTENDED_HISTORY
setopt HIST_IGNORE_DUPS

export HISTFILE=$HOME/.zhistory
export HISTSIZE=100000
export SAVEHIST=100000

function history-search-peco() {
  BUFFER=`history -n 1 | tail -r | awk '!a[$0]++' | peco`
  CURSOR=$#BUFFER
  zle reset-prompt
}

zle -N history-search-peco
bindkey '^R' history-search-peco

repo () {
  repository="$( ghq list | peco )"
  if [[ -n $repository ]]; then
    fullpath="$( ghq root )/$repository"
    cd $fullpath
  else
    echo "No repository selected."
  fi
}

gsw () {
  branch="$( git branch --format '%(refname:short)' | peco )"
  if [[ -n $branch ]]; then
    git switch $branch
  else
    echo "No branch selected."
  fi
}

gbd () {
  branch="$( git branch --format '%(refname:short)' | peco )"
  if [[ -n $branch ]]; then
    git branch -d $branch
  else
    echo "No branch selected."
  fi
}

gwt () {
  if [[ -n $1 ]]; then
    local main_root="$(git rev-parse --show-toplevel)"
    local main_settings="$main_root/.claude/settings.local.json"
    git wt "$1"
    if [[ -f "$main_settings" ]]; then
      mkdir -p .claude
      ln -sf "$main_settings" .claude/settings.local.json
      echo "Claude settings linked: .claude/settings.local.json -> $main_settings"
    fi
  else
    branch="$( git wt | tail -n +2 | peco | awk '{print $(NF-1)}' )"
    if [[ -n $branch ]]; then
      git wt $branch
    else
      echo "No worktree selected."
    fi
  fi
}

gwd () {
  branch="$( git wt | tail -n +2 | peco | awk '{print $(NF-1)}' )"
  if [[ -n $branch ]]; then
    git wt -d $branch
  else
    echo "No worktree selected."
  fi
}

zellij_tab_name_update() {
  if [[ -n $ZELLIJ ]]; then
    tab_name=''
    if git rev-parse --is-inside-work-tree >/dev/null 2>&1; then
      tab_name+=$(basename "$(git rev-parse --show-toplevel)")/
      tab_name+=$(git rev-parse --show-prefix)
      tab_name=${tab_name%/}
    else
      tab_name=$PWD
      if [[ $tab_name == $HOME ]]; then
        tab_name="~"
      else
        tab_name=${tab_name##*/}
      fi
    fi
    command nohup zellij action rename-tab $tab_name >/dev/null 2>&1
  fi
}

zellij_tab_name_update
chpwd_functions+=(zellij_tab_name_update)

alias cc="claude"

command -v starship &>/dev/null && eval "$(starship init zsh)"

if [[ -f $(brew --prefix)/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]]; then
  source $(brew --prefix)/share/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local
